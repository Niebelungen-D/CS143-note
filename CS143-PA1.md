#  CS143-PA1：熟悉cool语言

该作业为斯坦福大学编译原理公开课的第一部分，熟悉cool语言。具体要求可以从handouts中可以找到。

## 作业要求

![image-20210930181531131](https://raw.githubusercontent.com/Niebelungen-D/Imgbed-blog/main/mac-img/202109301815216.png)

这个`PA`的要求在`handouts/PA1.pdf`中。我们需要实现一个Stack Machine，这个机器以栈为存储和执行的基础。

启动栈机器后，机器创造一个命令行空间，在终端显示一个`>`，可以接受以下指令，这些指令都被压入到栈中。

- 整数
- 字符`+, s, e`

输入字符`e`，根据栈顶的内容执行相应的命令。

若栈顶为`+`，则将`+`和之后的两个整数弹出，两个整数相加后的结果压栈。

若栈顶为`s`，则将`s`弹出，再将之后的两个元素互换在栈中的位置。

若栈顶为一个整数，或栈为空，不进行任何操作。

输入字符`x`，退出这个栈机器。不要使用abort()退出。

## 思路与解答
笔者面向对象编程的经验极其有限，外加cool语言本身对一些操作的不支持，思考了很久通过怎样的结构来组成栈。
从给出的说明得知栈中存在整数和字符，但是cool本身没有联合体，我暴力的将其统一归为String类。在计算的时候使用A2I类的方法进行转换。为了让每个栈帧连接起来，我们还需要指针域，它指向下一个节点。通过将内容都归为String，不再需要对节点进行分类。
我使用Node作为栈祯，其结构如下：

```c++
class Node {
	data : String;
	next : Node;

	init(s:String, n :Node) : Node {
		{
		data <- s;
		next <- n;
		self;}
	};

	get_data() : String {
		data
	};

	get_next() : Node {
		next
	};

	isEnd() : Bool {
		if get_data() = "$" then
			true
		else 
			false
		fi
	};

};
```
有了栈帧，还需要对栈进行操作的函数，我将这部分函数定义为Stack类，用于对传入的Node参数进行操作。
```c++
class Stack inherits A2I {
	x : Int;
	y : Int;

	init(n : Node) : Node {
		(new Node).init("$",n)
	};

	pop(n : Node) : Node {
		n.get_next()
	};

	push(n : Node, d : String) : Node {
		(new Node).init(d, n)
	};

	add(n : Node) : Node {
		{
			x <- a2i(n.get_data());
			n <- n.get_next();
			y <- a2i(n.get_data());
			n <- n.init(i2a(x+y),n.get_next());
			n;
		}
	};

	swap(h : Node,l : Node) : Node {
		{
			h <- h.init(h.get_data(),l.get_next());
			l <- l.init(l.get_data(),h);
			l;
		}
	};

	exec(n : Node) : Node {		
		let cmd : String <- n.get_data() in 
		(
			if (cmd = "+") then
			{
				n <- n.get_next();
				n <- add(n);
				n;
			}
			else if (cmd = "s") then
			{
				n <- n.get_next();
				n <- swap(n, n.get_next());
				n;
			}
			else n
			fi fi
		)
	};

};
```
在做完这些后，main函数只要接收输入并调用即可：
```c++
class Main inherits IO {
	top : Node <- new Node;
	mystack : Stack <- new Stack;
	tmp :  String;

   print_stack(l : Node) : Object {
      	if l.isEnd() then 
	  		out_string("\n")
    	else {
			out_string(l.get_data());
			out_string("\n");
			print_stack(l.get_next());
		}
      fi
   };

	main() : Object{
		{
			top <- mystack.init(top);
			(let continue :Bool <- true in 
				while continue loop 
		{
			out_string(">");
			tmp <- in_string();
			if (tmp = "x") then 
				continue <- false
			else if (tmp = "d") then
				print_stack(top)
			else if (tmp = "e") then
				top <- mystack.exec(top)
			else 
				top <- mystack.push(top,tmp)
			fi fi fi;
		} pool
			);		
		top;
		}
	};
	
};
```

如果确定了要使用的数据结构，整个程序并不是很难。不过，cool语言的编译器并不是十分好用，如果出现了语法错误，通过编译器的报错根本无法确定bug。所以建议在做作业之前，阅读handouts中提供的参考手册和指南。
